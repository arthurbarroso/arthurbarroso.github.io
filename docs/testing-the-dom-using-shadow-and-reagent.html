<!DOCTYPE html>
<html lang="en">
  <head>
    <link rel="shorcut icon" href="favicon.ico"/>
    <link rel="icon" href="favicon.ico" />
    <meta charset='utf-8'>
    <link rel="stylesheet"
          href="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/10.3.1/styles/solarized-light.min.css">
    <link rel="preconnect" href="https://fonts.googleapis.com">
    <link rel="preconnect" href="https://fonts.gstatic.com" crossorigin>
    <link href="https://fonts.googleapis.com/css2?family=Roboto:ital,wght@0,400;0,700;1,400&display=swap" rel="stylesheet">
    <link rel="preconnect" href="https://fonts.gstatic.com" crossorigin>
    <link href="https://fonts.googleapis.com/css2?family=Fira+Code:wght@400;500&display=swap" rel="stylesheet">
    <script src="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/11.2.0/highlight.min.js"></script>
    <style>
      body {
        margin: 0;
        outline: 0;
        padding: 4% 10%;
        -webkit-font-smoothing: antialiased;
        -moz-osx-font-smoothing: grayscale;
        font-family: 'Roboto', sans-serif;

        pre > code {
          font-family: 'Fira Code', monospace;
        }

      }
    </style>
    <title>testing the dom using shadow and reagent - arthur barroso</title>
  </head>
  <body>
    <div id="post">
    <h1>Testing the DOM using shadow-cljs and Reagent</h1><p>I've been recently trying to build <a href='https://github.com/arthurbarroso/brundij'>brundij</a>, an open-source tool for squad health checks. I ended up deciding on using Clojure and Clojurescript to build it (I've been learning clojure for a few months already but feel like a complete beginner, so I thought building something might help me grasp some stuff). I also decided to use <code>shadow-cljs</code> to set up my project.</p><p>Everything was going pretty smoothly until I needed to run DOM tests, which seemed like a big mistery to me -it seemed like most CLJS developers were only using Cypress and the <a href='https://francisvitullo.medium.com/a-way-of-testing-views-in-clojurescript-apps-98aaf57c5c2a'>only post I could find talking about <code>react-testing-library</code> in clojurescript</a> was two years old. All of this led me into creating this post, in which, I am going to try to help Clojurescript beginners, like me, run DOM tests on their shadow-cljs + reagent applications.</p><h3>First try</h3><p>We'll start with a very basic reagent + shadow-cljs project. This will be done by running <code>lein new re-frame app</code> for simplicity's sake. With the app ready to go, we will also create a simple button component. It will look like this:<pre><code class="clojure">;;src/app/components/button.cljs
&#40;ns app.components.button&#41;

&#40;defn button &#91;{:keys &#91;on-click text disabled&#93;}&#93;
  &#91;:button
   {:type &quot;button&quot;
    :disabled disabled
    :on-click #&#40;on-click&#41;}
   text&#93;&#41;
</code></pre></p><p>This is a very basic dummy component. It receives an <code>on-click</code> handler, a <code>text</code>, and a <code>disabled</code> boolean check as properties. With our button set-up, let's try rendering it in our tests. We'll first need to add the following build specification to <code>shadow-cljs.edn</code> and then create our test file<pre><code class="clojure">;;shadow-cljs.edn
  :test {:target :browser-test
         :test-dir &quot;resources/public/js/test&quot;
         :devtools  {:http-port          8021
                     :http-root          &quot;resources/public/js/test&quot;}}}}
</code></pre></p><pre><code class="clojure">;;test/app/button&#95;test.cljs
&#40;ns app.button-test
  &#40;:require &#91;app.components.button :refer &#91;button&#93;&#93;
            &#91;cljs.test :refer-macros &#91;deftest is testing&#93;&#93;
            &#91;reagent.dom :as rdom&#93;&#41;&#41;

&#40;deftest button-component-test
  &#40;testing &quot;Renders correctly&quot;
    &#40;rdom/render &#91;button {:on-click #&#40;println &quot;hi&quot;&#41;
                          :text &quot;button&quot;
                          :disabled false}&#93;
                 &#40;.getElementById js/document &quot;app&quot;&#41;&#41;
    &#40;is &#40;= true true&#41;&#41;&#41;&#41;

</code></pre><p>If we run <code>npx shadow-cljs watch test</code> our test environment will be up and running. Assuming you've set up the test build config the same way I did, you'll be able to navigate to <code>localhost:8021</code> and check that our test is failing with an uncaught exception: <code>Error: target container is not a DOM element</code></p><p>This happens because there is no <code>div id=&quot;app&quot;</code> inside our test html file. We could change its content and insert a <code>div</code> with the id <code>app</code> directly to the file, but we'll do this another way.</p><h3>Getting things to render</h3><p>As seen above, we need to somehow have a div with the id <code>app</code> in our DOM before running the tests. A nice way of dealing with this is by using Clojurescript's <code>use-fixtures</code> -using it we'd be able to define a fixture that runs only once and uses javascript to create the div we need. Let's take a look at how we're able to do this:<pre><code class="clojure">;;test/app/button&#95;test.cljs
&#40;ns app.button-test
  &#40;:require &#91;app.components.button :refer &#91;button&#93;&#93;
            &#91;cljs.test :refer-macros &#91;deftest is testing use-fixtures&#93;&#93;
            &#91;reagent.dom :as rdom&#93;&#41;&#41;

&#40;defn create-app-element &#91;f&#93;
  &#40;.appendChild &#40;.-body js/document&#41; ;; gets the Body element
                &#40;doto &#40;.createElement js/document &quot;div&quot;&#41; ;; creates a new div
                  &#40;-&gt; &#40;.setAttribute &quot;id&quot; &quot;app&quot;&#41;&#41; ;; sets the new div id to be `app`
                  &#40;-&gt; &#40;.setAttribute &quot;style&quot; &quot;display:none;&quot;&#41;&#41;&#41;&#41; ;; makes that div invisible
  &#40;f&#41;&#41;

&#40;use-fixtures :once create-app-element&#41;

&#40;deftest button-component-test
  &#40;testing &quot;Renders correctly&quot;
    &#40;rdom/render &#91;button {:on-click #&#40;println &quot;hi&quot;&#41;
                          :text &quot;button&quot;
                          :disabled false}&#93;
                 &#40;.getElementById js/document &quot;app&quot;&#41;&#41;
    &#40;is &#40;= true true&#41;&#41;&#41;&#41;
</code></pre></p><p>We'll use this <code>create-app-element</code> function for each of our test files. It'll ensure there is the div with the id <code>app</code>. It basically creates a new div, set it's id to <code>app</code>, and set its <code>display</code> css property to <code>none</code>, so the components don't end up showing on our shadow test report page. This setup should be enough to get our tests running without the previous failure.</p><h3>Making assertions about components</h3><p>With components rendering in our tests, we're now able to make assertions about them. Let's say we want to check whether our button component actually renders. We could check if the <code>text</code> prop is being rendered within the button component using the following test</p><pre><code class="clojure">&#40;deftest button-component-test
  &#40;testing &quot;Renders correctly&quot;
    &#40;rdom/render &#91;button {:on-click #&#40;println &quot;hi&quot;&#41;
                          :text &quot;button&quot;
                          :disabled false}&#93;
                 &#40;.getElementById js/document &quot;app&quot;&#41;&#41;
    &#40;let &#91;app-element &#40;.getElementById js/document &quot;app&quot;&#41;
          button &#40;-&gt; &#40;.getElementsByTagName app-element &quot;button&quot;&#41;
                     &#40;first&#41;&#41;&#93;
      &#40;is &#40;= &quot;button&quot; &#40;.-textContent button&#41;&#41;&#41;&#41;&#41;&#41;
</code></pre><p>This is a dummy test. We're simply checking if things are rendering. If we really want to test things and their behaviors we'd need to click buttons, change inputs and such. This is where <code>react-dom/test-utils</code> comes in.</p><p>With <code>react-dom/test-utils</code>, we're able to simulate user events and check whether our button uses the <code>on-click</code> handler property it receives. An example of test using <code>react-dom/test-utils</code> would look like the following:<pre><code class="clojure">&#40;ns app.button-test
  &#40;:require &#91;app.components.button :refer &#91;button&#93;&#93;
            &#91;cljs.test :refer-macros &#91;deftest is testing use-fixtures&#93;&#93;
            &#91;reagent.core :as reagent&#93;
            &#91;reagent.dom :as rdom&#93;
            &#91;&quot;react-dom/test-utils&quot; :as dom-test-utils&#93;&#41;&#41;

&#40;defn create-app-element &#91;f&#93;
  &#40;.appendChild &#40;.-body js/document&#41;
                &#40;doto &#40;.createElement js/document &quot;div&quot;&#41;
                  &#40;-&gt; &#40;.setAttribute &quot;id&quot; &quot;app&quot;&#41;&#41;
                  &#40;-&gt; &#40;.setAttribute &quot;style&quot; &quot;display:none;&quot;&#41;&#41;&#41;&#41;
  &#40;f&#41;&#41;

&#40;use-fixtures :once create-app-element&#41;

&#40;deftest button-component-click-test
  &#40;testing &quot;Uses the supplied `on-click` property&quot;
    &#40;let &#91;ra &#40;reagent/atom 1&#41;&#93; ;; we create a new atom with the value of 1
      &#40;rdom/render &#91;button {:on-click #&#40;swap! ra inc&#41; ;; we define that 
                                                      ;;on-click should increment 
                                                      ;;our atom's value
                            :text &quot;button&quot;
                            :disabled false}&#93;
                   &#40;.getElementById js/document &quot;app&quot;&#41;&#41;
      &#40;let &#91;app-element &#40;.getElementById js/document &quot;app&quot;&#41;
            button &#40;-&gt; &#40;.getElementsByTagName app-element &quot;button&quot;&#41;
                       &#40;first&#41;&#41;&#93; ;; gets the button element
        &#40;.click dom-test-utils/Simulate button&#41; ;; react-dom/test-utils simulates a user click
        &#40;is &#40;= 2 @ra&#41;&#41;&#41;&#41;&#41;&#41; ;; ra's value should've been incremented
</code></pre></p><p>This example uses a reagent atom to check whether on-click has been called. This could also be done using re-frame, for example.</p><h3>Cleaning up the DOM in between tests</h3><p>Let's say we want to clean up the DOM between each <code>deftest</code>. This is achievable, but not with the code we currently have. Since we're using <code>reagent-dom/render</code> to render stuff, we cant simply delete our <code>app</code>'s div children -our components aren't being rendered as children, the whole <code>app</code> div is becoming our components. We can fix this.</p><p>Our first step will be defining our <code>append-container</code> function. This function will take a target element and an <code>id</code> as arguments. It will then create a div with that <code>id</code> as the <code>target</code>'s children. We will then define our <code>dom-cleanup!</code> fixture, which will use's <code>clojure.browser.dom</code> <code>remove-children</code> function, which simply removes a DOM element's children.<pre><code class="clojure">&#40;ns app.button-test
  &#40;:require &#91;app.components.button :refer &#91;button&#93;&#93;
            &#91;cljs.test :refer-macros &#91;deftest is testing use-fixtures&#93;&#93;
            &#91;clojure.browser.dom :refer &#91;remove-children&#93;&#93;
            &#91;reagent.core :as reagent&#93;
            &#91;reagent.dom :as rdom&#93;
            &#91;&quot;react-dom/test-utils&quot; :as dom-test-utils&#93;&#41;&#41;

&#40;defn create-app-element &#91;f&#93;
  &#40;.appendChild &#40;.-body js/document&#41;
                &#40;doto &#40;.createElement js/document &quot;div&quot;&#41;
                  &#40;-&gt; &#40;.setAttribute &quot;id&quot; &quot;app&quot;&#41;&#41;
                  &#40;-&gt; &#40;.setAttribute &quot;style&quot; &quot;display:none;&quot;&#41;&#41;&#41;&#41;
  &#40;f&#41;&#41;

&#40;defn dom-cleanup! &#91;f&#93;
  &#40;remove-children &quot;app&quot;&#41;
  &#40;f&#41;&#41;

&#40;defn append-container &#91;target id&#93;
  &#40;let &#91;container &#40;.getElementById js/document id&#41;&#93;
    &#40;if container
      container
      &#40;.appendChild target &#40;doto &#40;.createElement js/document &quot;div&quot;&#41;
                             &#40;-&gt; &#40;.setAttribute &quot;id&quot; id&#41;&#41;&#41;&#41;&#41;&#41;&#41;

&#40;use-fixtures :once create-app-element&#41;
&#40;use-fixtures :each dom-cleanup!&#41;
</code></pre></p><p>Our tests will stay pretty much the same, except we'll render them using <code>append-container</code> and will query the DOM for them using the <code>id</code> we supply to <code>append-container</code><pre><code class="clojure">&#40;deftest button-component-click-test
  &#40;testing &quot;Uses the supplied `on-click` property&quot;
    &#40;let &#91;ra &#40;reagent/atom 1&#41;&#93;
      &#40;rdom/render &#91;button {:on-click #&#40;swap! ra inc&#41;
                            :text &quot;button&quot;
                            :disabled false}&#93;
                   &#40;append-container &#40;.getElementById js/document &quot;app&quot;&#41;
                                     &quot;button-click-test&quot;&#41;&#41; ;; this creates a children div
                                                           ;; with the id `button-click-test`
      &#40;let &#91;app-element &#40;.getElementById js/document &quot;button-click-test&quot;&#41; ;; queries for
                                                                          ;; the div we've
                                                                          ;; just created
            button &#40;-&gt; &#40;.getElementsByTagName app-element &quot;button&quot;&#41;
                       &#40;first&#41;&#41;&#93;
        &#40;.click dom-test-utils/Simulate button&#41;
        &#40;is &#40;= 2 @ra&#41;&#41;&#41;&#41;&#41;&#41;
</code></pre></p><h3>Setting up Karma</h3><p>In order to run the tests we've just created in a CI, we'll need <a href='https://karma-runner.github.io/latest/index.html'>karma</a>. Karma is a javascript test runner and is recommended by the shadow-clj's user guide. Let's set it up. To do so, we'll add karma's dependencies to the <code>package.json</code> <code>devDependencies</code>, tweak our <code>shadow-cljs.edn</code> file a little and create a <code>karma.conf.js</code> file. Let's go ahead.</p><p>We'll need <code>karma</code>, <code>karma-cljs-test</code> (so we can write our tests using <code>cljs</code>) and <code>karma-chrome-launcher</code>, so karma spins up a chrome instance and runs our browser tests.<pre><code class="json">//package.json
{
	&quot;devDependencies&quot;: {
		&quot;shadow-cljs&quot;: &quot;2.15.2&quot;,
    &quot;karma&quot;: &quot;&#94;2.0.0&quot;,
    &quot;karma-chrome-launcher&quot;: &quot;&#94;2.2.0&quot;,
    &quot;karma-cljs-test&quot;: &quot;&#94;0.1.0&quot;
	}
}

</code></pre></p><p>We'll add a <code>:ci</code> build configuration in our shadow-cljs.edn file.<pre><code class="edn"> ;;shadow-cljs.edn
  :ci {:target :karma
       :output-to &quot;target/ci.js&quot;}}}
</code></pre></p><pre><code class="js">//karma.conf.js
module.exports = function &#40;config&#41; {
    config.set&#40;{
        browsers: &#91;'ChromeHeadless'&#93;,
        // The directory where the output file lives
        basePath: 'target',
        // The file itself
        files: &#91;'ci.js'&#93;,
        frameworks: &#91;'cljs-test'&#93;,
        plugins: &#91;'karma-cljs-test', 'karma-chrome-launcher'&#93;,
        colors: true,
        logLevel: config.LOG&#95;INFO,
        client: {
            args: &#91;&quot;shadow.test.karma.init&quot;&#93;,
            singleRun: true
        }
    }&#41;
};

</code></pre><p>We're now able to run our tests in a CI environment using <code>npx shadow-cljs compile ci &amp;&amp; npm run karma start --single-run</code></p><h3>React testing library</h3><p>I won't really write much aobut <code>react-testing-library</code> in here. There is a <a href='https://francisvitullo.medium.com/a-way-of-testing-views-in-clojurescript-apps-98aaf57c5c2a'>great post by Francesco Vitullo on how to test Clojurescript apps using it</a>. I tried following it's steps (pinning react-testing-library's version to <code>6.1.2</code>) and managed to get it working. I think it's main advantage is that RTL makes it possible to avoid all that DOM manipulation we've done, but this didn't seem like a good enough reason for me to use it. I plan on giving it another try, though.</p><p><i>09-12-2021</i></p>
    </div>
    <script>hljs.highlightAll();</script>
  </body>
</html>
